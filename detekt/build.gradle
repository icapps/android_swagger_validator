plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'maven-publish'
}

apply plugin: 'com.jfrog.bintray'

def artifactVersion = '0.1.0'
def artifactGroup = 'com.icapps.swaggervalidator'
def artifactName = "swaggervalidator-detekt"

group "$artifactGroup"
version "$artifactVersion"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "io.gitlab.arturbosch.detekt:detekt-api:1.2.1"
    implementation 'io.swagger:swagger-parser:1.0.48'
    implementation 'com.squareup.okhttp3:okhttp:4.2.2'
    implementation 'net.harawata:appdirs:1.0.3'
}

bintray {
    user = System.getenv('BINTRAY_USER_ICAPPS')
    key = System.getenv('BINTRAY_KEY_ICAPPS')
    pkg {
        repo = 'maven'
        name = "$artifactName"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/icapps/android_swagger_validator'

        version {
            name = "$artifactVersion"
            desc = "Runtime for the android swagger validator"
            released = new Date()
        }
    }
    publications = ['mavenJava']
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = artifactName
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Swagger validator'
                description = 'A detekt plugin that validates annotated models with a swagger spec'
                developers {
                    developer {
                        id = 'nicolaverbeeck'
                        name = 'Nicola Verbeeck'
                    }
                    developer {
                        id = 'maartenvangiel'
                        name = 'Maarten Van Giel'
                    }
                }
            }
        }
    }
}