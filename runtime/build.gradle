plugins {
    id 'java'
    id 'maven-publish'
}

apply plugin: 'com.jfrog.bintray'

def artifactVersion = '0.1.0'
def artifactGroup = 'com.icapps.swaggervalidator'
def artifactName = "swaggervalidator-runtime"

group "$artifactGroup"
version "$artifactVersion"

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = System.getenv('BINTRAY_USER_ICAPPS')
    key = System.getenv('BINTRAY_KEY_ICAPPS')
    pkg {
        repo = 'maven'
        name = "$artifactName"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/icapps/android_swagger_validator'

        version {
            name = "$artifactVersion"
            desc = "Runtime for the android swagger validator"
            released = new Date()
        }
    }
    publications = ['mavenJava']
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = artifactName
            from components.java

            artifact sourceJar
            artifact javadocJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Swagger validator - runtime'
                description = 'Runtime for the swagger validator, annotate your swagger classes!'
                developers {
                    developer {
                        id = 'nicolaverbeeck'
                        name = 'Nicola Verbeeck'
                    }
                    developer {
                        id = 'maartenvangiel'
                        name = 'Maarten Van Giel'
                    }
                }
            }
        }
    }
}